* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/***********************************************************************

/*  File: TESTNU

/*  NATUNIT's main test program for running all internal tests.

/***********************************************************************
DEFINE DATA

LOCAL USING NUCONST
LOCAL USING NUCRSTAP

LOCAL USING NUPARSEP
LOCAL USING NUTCP

LOCAL
01 #T (I4)
01 #TC (I4)
01 #RESULT (A) DYNAMIC
01 #ALL-TESTS-SUCCESSFUL (L)

END-DEFINE

NUPARSEP.LIBNAME := *LIBRARY-ID
NUPARSEP.TESTCASENAME := 'TSSUITE1'

PERFORM NU-PARSE NUPARSEP

IF NOT NUPARSEP.VALID
  NUTCP.TC-RESULT := C-NU-ERROR
  NUTCP.TC-MESSAGE := NUPARSEP.MESSAGE
END-IF

IF *OCCURRENCE(NUPARSEP.TEST) EQ 0
  NUTCP.TC-RESULT := C-NU-ERROR
  COMPRESS 'TestCase <' NUPARSEP.TESTCASENAME '> contained no tests.' INTO NUTCP.TC-MESSAGE
END-IF

IF NUTCP.TC-RESULT NE C-NU-ERROR
  PERFORM NU-RUN-TESTCASE NUPARSEP NUTCP
END-IF

PERFORM NU-SHOW-STATISTICS NUTCP

IF NUTCP.TC-RESULT NE C-NU-SUCCESS
  PERFORM NU-SHOW-PROBLEMS NUTCP
  WRITE 'NATUNIT self-test FAILED.'
ELSE
  WRITE 'All NATUNIT self-tests were SUCCESSFUL.'
END-IF

END

