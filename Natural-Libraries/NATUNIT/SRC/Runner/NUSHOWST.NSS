* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/***********************************************************************

/*  File: NUSHOWST

/*  Shows NATUNIT's statistics (aka the red bar).

/*  Parameters:
/*    NUTCP - The test results.

/***********************************************************************
DEFINE DATA

PARAMETER USING NUTCP

LOCAL USING NUCRSTAP
LOCAL USING NUCONST

LOCAL
01 #FORMAT-RESULTS (C) INIT <(AD=D)>

01 #C-NUM-ROWS (N2) CONST <19>
01 #C-NUM-COLS (N2) CONST <79>

01 #TC (I4)
01 #ROW (I4)
01 #RESULTS (A79/1:#C-NUM-ROWS)

01 #DIVIDEND (N4)
01 #REMAINDER (N2,2)

01 #NEW-ROW (L)

01 #RESULT-SYMBOLS (A1/1:7)
01 #CURRENT-TC-RESULT (N1)
01 #CURRENT-TC-FAILED-OR-ERROR (L)

01 #I (I4)
01 #STAT-INDEX (N2)
01 #BATCH-STATS (A/1:*) DYNAMIC
01 #CURRENT-TC (A8)
01 #LAST-ERROR-TC (A8)

END-DEFINE

/***********************************************************************
DEFINE SUBROUTINE NU-SHOW-STATISTICS
/***********************************************************************

#RESULT-SYMBOLS(C-NU-SUCCESS) := '.'
#RESULT-SYMBOLS(C-NU-SKIPPED) := 'S'
#RESULT-SYMBOLS(C-NU-FAILURE) := 'F'
#RESULT-SYMBOLS(C-NU-ERROR) := 'E'
#RESULT-SYMBOLS(C-NU-SETUP-TESTCASE) := 'B'
#RESULT-SYMBOLS(C-NU-TEARDOWN-TESTCASE) := 'A'
#RESULT-SYMBOLS(C-NU-UNKNOWN) := 'N'

PERFORM NU-CREATE-STATISTICS NUTCP NUCRSTAP

RESET #RESULTS(*)
#NEW-ROW := TRUE
#CURRENT-TC-FAILED-OR-ERROR := FALSE

FOR #TC 1 *OCCURRENCE(NUTCP.TESTCASE)

  #CURRENT-TC := NUTCP.TESTCASE(#TC)

  IF #NEW-ROW

    ADD 1 TO #ROW
    #NEW-ROW := FALSE

    #DIVIDEND := #ROW
    DIVIDE #C-NUM-ROWS INTO #DIVIDEND REMAINDER #REMAINDER
    IF #REMAINDER EQ 1
      #ROW := 1
      RESET #RESULTS(*)
    END-IF

  END-IF

  #DIVIDEND := #TC
  DIVIDE #C-NUM-COLS INTO #DIVIDEND REMAINDER #REMAINDER
  IF #REMAINDER EQ 0
    #NEW-ROW := TRUE
  END-IF

  #CURRENT-TC-RESULT := NUTCP.TEST-RESULT(#TC)
  IF #CURRENT-TC-RESULT EQ C-NU-ERROR 
      OR #CURRENT-TC-RESULT EQ C-NU-FAILURE
    #CURRENT-TC-FAILED-OR-ERROR := TRUE
    #LAST-ERROR-TC := NUTCP.TESTCASE(#TC)
  END-IF

  IF #CURRENT-TC-RESULT < C-NU-SKIPPED
      OR #CURRENT-TC-RESULT > C-NU-TEARDOWN-TESTCASE
    #CURRENT-TC-RESULT := C-NU-UNKNOWN
  END-IF

  COMPRESS #RESULTS(#ROW) #RESULT-SYMBOLS(#CURRENT-TC-RESULT)
    INTO #RESULTS(#ROW) LEAVING NO

END-FOR

IF #CURRENT-TC-FAILED-OR-ERROR
  MOVE (AD=I) TO #FORMAT-RESULTS
END-IF

INPUT USING MAP 'NUSTAT'

END-SUBROUTINE

END

