* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/***********************************************************************

/*  File: NUNITRUN

/*  Runs a TestCase for the current user in the current library.

/***********************************************************************
DEFINE DATA

PARAMETER

01 P-TESTCASE-NAME (A8) BY VALUE
01 P-REPORT-FILE (A) DYNAMIC BY VALUE
01 P-RC (I4) BY VALUE RESULT

LOCAL USING NUCONST
LOCAL USING NUPARSEP
LOCAL USING NUTCP

LOCAL
01 #XML (A) DYNAMIC
01 #TC-PREFIX (A) DYNAMIC

END-DEFINE

NUPARSEP.LIBNAME := *LIBRARY-ID
NUPARSEP.TESTCASENAME := P-TESTCASE-NAME
PERFORM NU-PARSE NUPARSEP

RESET NUTCP
IF NOT NUPARSEP.VALID
  NUTCP.TC-RESULT := C-NU-ERROR
  NUTCP.TC-MESSAGE := NUPARSEP.MESSAGE
END-IF
IF *OCCURRENCE(NUPARSEP.TEST) EQ 0
  NUTCP.TC-RESULT := C-NU-ERROR
  COMPRESS 'TestCase <' NUPARSEP.TESTCASENAME '> contained no tests.' INTO NUTCP.TC-MESSAGE
END-IF

IF NUTCP.TC-RESULT NE C-NU-ERROR
  PERFORM NU-RUN-TESTCASE NUPARSEP NUTCP
END-IF

PERFORM NU-CREATE-PREFIX #TC-PREFIX
PERFORM NU-CREATE-XML-OUTPUT NUTCP #XML #TC-PREFIX

DEFINE WORK FILE 10 P-REPORT-FILE TYPE 'UNFORMATTED'
WRITE WORK 10 VARIABLE #XML
CLOSE WORK 10

P-RC := NUTCP.TC-RESULT

END

