* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
/***********************************************************************

/*  File: NUWRITE

/*  Handles NATUNIT's output.

/*  Parameters:
/*    P-TEXT - The output text

/***********************************************************************
DEFINE DATA

PARAMETER

01 P-TEXT (A) DYNAMIC BY VALUE

INDEPENDENT

01 +AVI-OUTPUT-INITIALIZED (L)
01 +AVI-OUTPUT-DEVICE (I4)
01 +AVI-OUTPUT-FILENAME (A) DYNAMIC

LOCAL USING NUCONST

LOCAL

01 #LINE (A) DYNAMIC
01 #LINE-LENGTH (I4)

01 #SPLIT-LINE (A/1:*) DYNAMIC
01 #NUM-PIECES (I4)
01 #PIECE (I4)
01 #REMAINDER (I4)

01 #SUBSTRING-START (I4)
01 #SUBSTRING-LENGTH (I4)

END-DEFINE

DEFINE SUBROUTINE NU-WRITE

PERFORM WRITE-UI
RESET P-TEXT

DEFINE SUBROUTINE WRITE-UI

#LINE := P-TEXT
#LINE-LENGTH := *LENGTH(#LINE)
IF #LINE-LENGTH LE 79
  IF *DEVICE EQ 'BATCH'
    SET CONTROL 'N'
  END-IF
  WRITE #LINE (AL=79)
ELSE
  #NUM-PIECES := #LINE-LENGTH - 79
  DIVIDE 74 INTO #NUM-PIECES REMAINDER #REMAINDER
  IF #REMAINDER GT 0
    ADD 1 TO #NUM-PIECES /* for last line
  END-IF

/* split long lines into multiple lines
  EXPAND ARRAY #SPLIT-LINE TO (1:#NUM-PIECES)
  FOR #PIECE 1 #NUM-PIECES
    #SUBSTRING-START := 80 + (#PIECE - 1) * 74
    #SUBSTRING-LENGTH := #LINE-LENGTH - #SUBSTRING-START + 1
    IF #SUBSTRING-LENGTH GT 74
      #SUBSTRING-LENGTH := 74
    END-IF

    #SPLIT-LINE(#PIECE) := SUBSTRING(#LINE, #SUBSTRING-START, #SUBSTRING-LENGTH)
  END-FOR

  IF *DEVICE EQ 'BATCH'
    SET CONTROL 'N'
  END-IF
  WRITE #LINE (AL=79)
  FOR #PIECE 1 #NUM-PIECES
    WRITE '    ' #SPLIT-LINE(#PIECE) (AL=74)
  END-FOR
END-IF

END-SUBROUTINE

END-SUBROUTINE

END

