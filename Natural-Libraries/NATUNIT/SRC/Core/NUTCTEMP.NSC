* >Natural Source Header 000000
* :Mode S
* :CP
* <Natural Source Header
IF NUTESTP.TEST-DATA-PARAM EQ C-NU-PARSE-TEST-DATA
  PERFORM TEST
  ESCAPE ROUTINE
END-IF

NUTESTP.RESULT := C-NU-SUCCESS
RESET NUTESTP.NUM-ASSERTS NUTESTP.MESSAGE
RESET NUASSP.MESSAGE

IF NUTESTP.RUN-SETUP-TESTCASE
  PERFORM SETUP-TESTCASE
  ESCAPE ROUTINE
END-IF
IF NUTESTP.RUN-TEARDOWN-TESTCASE
  PERFORM TEARDOWN-TESTCASE
  ESCAPE ROUTINE
END-IF

IF NUTESTP.HAS-SETUP
  PERFORM SETUP
END-IF

PERFORM TEST

/* handle ASSERT-EXPECTED-ERROR
IF NUASSP.EXPECTED-ERROR NE 0
  NUTESTP.RESULT := C-NU-FAILURE
  COMPRESS NUMERIC '(' ASSERT-LINE ') failed:' INTO NUTESTP.MESSAGE LEAVING NO
  COMPRESS *PROGRAM NUTESTP.MESSAGE *TRIM(NUASSP.MESSAGE) INTO NUTESTP.MESSAGE
  COMPRESS NUTESTP.MESSAGE 'but no Natural error occurred' INTO NUTESTP.MESSAGE
END-IF

IF NUTESTP.HAS-TEARDOWN
  PERFORM TEARDOWN
END-IF

PERFORM LOG

DEFINE SUBROUTINE LOG
NUTESTP.NUM-ASSERTS := NUASSP.NUM-ASSERTS
END-SUBROUTINE

ON ERROR
  /* handle ASSERT-EXPECTED-ERROR
  IF NUASSP.EXPECTED-ERROR NE 0
    IF *ERROR-NR EQ NUASSP.EXPECTED-ERROR
      NUTESTP.RESULT := C-NU-SUCCESS
    ELSE
      NUTESTP.RESULT := C-NU-FAILURE
      COMPRESS NUMERIC '(' ASSERT-LINE ') failed:' INTO NUTESTP.MESSAGE LEAVING NO
      COMPRESS *PROGRAM NUTESTP.MESSAGE *TRIM(NUASSP.MESSAGE) INTO NUTESTP.MESSAGE
      COMPRESS NUTESTP.MESSAGE 'but was Natural error' *ERROR-NR INTO NUTESTP.MESSAGE
    END-IF
  ELSE
    IF *ERROR-NR EQ C-NU-ASSERTION-ERROR
      NUTESTP.RESULT := C-NU-FAILURE
      COMPRESS NUMERIC '(' ASSERT-LINE ') failed:' INTO NUTESTP.MESSAGE LEAVING NO
      COMPRESS *PROGRAM NUTESTP.MESSAGE *TRIM(NUASSP.MESSAGE) INTO NUTESTP.MESSAGE
    ELSE
      NUTESTP.RESULT := C-NU-ERROR
      COMPRESS NUMERIC '(' *ERROR-LINE ') error:' INTO NUTESTP.MESSAGE LEAVING NO
      COMPRESS NUMERIC *PROGRAM NUTESTP.MESSAGE *ERROR-NR INTO NUTESTP.MESSAGE
      PERFORM NU-GET-CURRENT-ERROR-MESSAGE NUASSP.MESSAGE
      COMPRESS NUMERIC NUTESTP.MESSAGE NUASSP.MESSAGE INTO NUTESTP.MESSAGE
    END-IF
  END-IF
  IF NUTESTP.HAS-TEARDOWN
    PERFORM TEARDOWN
  END-IF
  PERFORM LOG
  ESCAPE MODULE
END-ERROR

